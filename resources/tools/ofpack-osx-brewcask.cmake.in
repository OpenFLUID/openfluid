###
### Usage: cmake [-DDOWNLOAD_STATUS=<status>] -P ofpack-osx-brewcask.cmake
###


SET(SOURCE_PATH @CMAKE_SOURCE_DIR@)
SET(BUILD_PATH @CMAKE_BINARY_DIR@)

SET(BREWCASK_DIR "_osx_brewcask")
SET(BREWCASK_PATH "${BUILD_PATH}/${BREWCASK_DIR}")


#### Packaging using CPack

MESSAGE(STATUS "Packaging")

FILE(REMOVE_RECURSE "${BREWCASK_PATH}")
FILE(MAKE_DIRECTORY "${BREWCASK_PATH}")

EXECUTE_PROCESS(COMMAND "cpack" "-G" "TBZ2" "-B" "${BREWCASK_PATH}")


#### Discovery of the created package

FILE(GLOB PACKFILE_PATH "${BREWCASK_DIR}/*${CASKFILE_VERSION}*.bz2")

LIST(LENGTH PACKFILE_PATH FOUND_PACKFILES_LEN)
IF(NOT (${FOUND_PACKFILES_LEN} EQUAL 1))
  MESSAGE(FATAL_ERROR "Error in package discovery (Found '${PACKFILE_PATH}')")
ENDIF()

GET_FILENAME_COMPONENT(PACKFILE_NAME ${PACKFILE_PATH} NAME)
MESSAGE(STATUS "Found file : ${PACKFILE_NAME}")


#### Generation of the Cask file(s)

IF(DOWNLOAD_STATUS)
  SET(CASKFILE_URL "@OPENFLUID_URL_OFFICIAL@/dloadsproxy/${DOWNLOAD_STATUS}/${PACKFILE_NAME}")
ELSE()
  SET(URL_VERSION "v@OPENFLUID_VERSION_MAJOR@.@OPENFLUID_VERSION_MINOR@.@OPENFLUID_VERSION_PATCH@")
  SET(CASKFILE_URL "@OPENFLUID_URL_OFFICIAL@/dloadsproxy/final/${URL_VERSION}/${PACKFILE_NAME}")
ENDIF()

SET(CASKFILE_VERSION "@OPENFLUID_VERSION_FULL@")
SET(CASKFILE_HOMEPAGE "@OPENFLUID_URL_OFFICIAL@")
SET(CASKFILE_OS_CODE "@OFBUILD_SYSTEM_CODE@")

FILE(SHA256 "${PACKFILE_PATH}" CASKFILE_PACKHASH)
MESSAGE(STATUS "Computed hash : ${CASKFILE_PACKHASH}")

FOREACH(TOKEN_VERSION "" "@OPENFLUID_VERSION_NUMERIC@")
  CONFIGURE_FILE(${SOURCE_PATH}/resources/packaging/macosx/openfluid.rb.in "${BREWCASK_PATH}/openfluid${TOKEN_VERSION}.rb")
  MESSAGE(STATUS "Generated Caskfile : ${BREWCASK_PATH}/openfluid${TOKEN_VERSION}.rb")
ENDFOREACH()  
  



